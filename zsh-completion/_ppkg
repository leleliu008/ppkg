#compdef ppkg

_commands=(
    '-h:show help of this command.'
    '--help:show help of this command.'
    '-V:show version of this command.'
    '--version:show version of this command.'
    'ls-available:list the available packages.'
    'ls-installed:list the installed packages.'
    'ls-outdated:list the installed packages which can be upgraded.'
    'is-available:is the given package available.'
    'is-installed:is the given package installed.'
    'is-outdated:is the given package outdated.'
    'search:search packages can be installed.'
    'tree:list files of the given installed package in a tree-like format.'
    'info:show infomation of the given package or all available packages.'
    'formula-repo-add:add a new formula-repo.'
    'formula-repo-del:delete a existing formula-repo.'
    'formula-repo-list: list all the registed formula-repos.'
    'install:install packages.'
    'reinstall:reinstall packages.'
    'uninstall:uninstall packages.'
    'cleanup:cleanup the unused cache.'
    'update:update the formula repository.'
    'upgrade:upgrade the outdated packages.'
    'upgrade-self:upgrade this software.'
    'fetch:download formula resources of the given package to the cache.'
    'integrate:integrate zsh completions.'
    'env:show current machine os and environment variables.'
    'depends:show the depends of the given package.'
    'pack:pack the given installed package.'
    'logs:show logs of the given installed package.'
    'gen-github-workflows:generate github workflows.'
)

function _ppkg_commands() {
    _describe 'command' _commands
}

function _ppkg_available_packages() {
    local -a _available_packages
    _available_packages=($(ppkg ls-available 2>/dev/null))
    _describe 'available-packages' _available_packages
}

function _ppkg_installed_packages() {
    local -a _installed_packages
    _installed_packages=($(ppkg ls-installed 2>/dev/null))
    _describe 'installed-packages' _installed_packages
}

function _ppkg_outdated_packages() {
    local -a _outdated_packages
    _outdated_packages=($(ppkg ls-outdated 2>/dev/null))
    _describe 'outdated-packages' _outdated_packages
}

function _ppkg() {
    local context state state_descr line
    typeset -A opt_args

    _arguments \
        '1: :_ppkg_commands' \
        '*:: :->option'

    case $state in
        option)
            case $words[1] in
                formula-repo-del)
                    _arguments '1:repo-name:($(ppkg formula-repo-list | cut -d " " -f1))'
                    ;;
                integrate)
                    _arguments '1:what:(zsh)'
                    ;;
                info)
                    if [ "$words[2]" = '@all' ] ; then
                        _arguments \
                            '1:package-name:(@all)' \
                            '2:output-format:(--json --yaml --shell)'
                    else
                        case $words[3] in
                            installed-*)
                                _arguments \
                                    '1:package-name:()' \
                                    '2:key:(installed-dir installed-files installed-receipt-yaml installed-receipt-json installed-timestamp-unix installed-timestamp-rfc-3339 installed-timestamp-iso-8601 installed-version)'
                                ;;
                            *)  _arguments \
                                    '1:package-name:(@all $(ppkg ls-available 2>/dev/null))' \
                                    '2:key:(formula summary version license web-url git-url git-sha src-url src-sha fix-url fix-sha res-url res-sha dep-pkg dep-upp cdefine ccflags xxflags ldflags bscript binbstd bsystem installed-dir installed-files installed-receipt-yaml installed-receipt-json installed-timestamp-unix installed-timestamp-rfc-3339 installed-timestamp-iso-8601 installed-version symlinked-files --json --yaml --shell)'
                        esac
                    fi
                    ;;
                fetch)
                    _arguments ':package-name:(@all $(ppkg ls-available 2>/dev/null))'
                    ;;
                depends)
                    _arguments \
                        '1:package-name:_ppkg_available_packages' \
                        '--format=-[set output format]:format:(dot box svg png)'
                    ;;
                pack)
                    _arguments \
                        '1:package-name:_ppkg_installed_packages' \
                        '--type=-[set output type]:output-type:(zip tar.gz tar.xz tar.lz tar.bz2)' \
                        '--keep-packing-dir[do not delete packing dir even if packed success]'
                    ;;
                tree)
                    _arguments \
                        '1:package-name:_ppkg_installed_packages' \
                        '-a[show all files, including hidden ones]' \
                        '--dirsfirst[list directories before files]' \
                        '-L[level]:level:(3)'
                    ;;
                logs)
                    _arguments \
                        '1:package-name:_ppkg_installed_packages'
                    ;;
                install)
                    _arguments \
                        ':package-name:(@all $(ppkg ls-available 2>/dev/null))' \
                        '--toolchain=-[set which toolchain to use]:toolchain:(zig gcc llvm)' \
                        '--jobs=-[set the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--dry-run[dry-run]' \
                        '--keep-installing-dir[do not delete installing dir even if installed success]' \
                        '--build-type=-[set build type]:build-type:(debug release)' \
                        '--link-type=-[link static or shared library]:link-type:(static-only shared-only static-prefered shared-prefered)' \
                        '--install-lib=-[install static or shared library]:install-lib:(static shared both yes no)' \
                        '--install-exe[install executable programs]' \
                        '--install-doc[install documents]' \
                        '--install-man[install manpages]' \
                        '--export-compile-commands-json[generates compile_commands.json]' \
                        '--disable-ccache[do not use ccache]' \
                        '--tree-args=-[set arguments of tree command]:tree-args:(--dirsfirst -a)' \
                        '--strip-args=-[set arguments of strip command]:strip-args:(--strip-debug --strip-unneeded)' \
                        + '(verbose_level)' \
                        '-vv[debug mode. many many messages will be output to terminal]' \
                        '-v[verbose mode. many messages will be output to terminal]' \
                        '-q[silent mode. no any messages will be output to terminal]'
                    ;;
                reinstall)
                    _arguments \
                        ':package-name:_ppkg_installed_packages' \
                        '--toolchain=-[set which toolchain to use]:toolchain:(zig gcc llvm)' \
                        '--jobs=-[set the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--dry-run[dry-run]' \
                        '--keep-installing-dir[do not delete installing dir even if installed success]' \
                        '--build-type=-[set build type]:build-type:(debug release)' \
                        '--link-type=-[link static or shared library]:link-type:(static-only shared-only static-prefered shared-prefered)' \
                        '--install-lib=-[install static or shared library]:install-lib:(static shared both yes no)' \
                        '--install-exe[install executable programs]' \
                        '--install-doc[install documents]' \
                        '--install-man[install manpages]' \
                        '--export-compile-commands-json[generates compile_commands.json]' \
                        '--disable-ccache[do not use ccache]' \
                        '--tree-args=-[set arguments of tree command]:tree-args:(--dirsfirst -a)' \
                        '--strip-args=-[set arguments of strip command]:strip-args:(--strip-debug --strip-unneeded)' \
                        + '(verbose_level)' \
                        '-vv[debug mode. many many messages will be output to terminal]' \
                        '-v[verbose mode. many messages will be output to terminal]' \
                        '-q[silent mode. no any messages will be output to terminal]'
                    ;;
                upgrade)
                    _arguments \
                        ':package-name:_ppkg_outdated_packages' \
                        '--jobs=-[set the number of jobs you can run in parallel]:jobs:(1 2 3 4 5 6 7 8 9)' \
                        '--dry-run[dry-run]' \
                        '--keep-installing-dir[do not delete installing dir even if installed success]' \
                        '--build-type=-[set build type]:build-type:(debug release)' \
                        '--link-type=-[link static or shared library]:link-type:(static-only shared-only static-prefered shared-prefered)' \
                        '--install-lib=-[install static or shared library]:install-lib:(static shared both yes no)' \
                        '--install-exe[install executable programs]' \
                        '--install-doc[install documents]' \
                        '--install-man[install manpages]' \
                        '--export-compile-commands-json[generates compile_commands.json]' \
                        '--disable-ccache[do not use ccache]' \
                        '--tree-args=-[set arguments of tree command]:tree-args:(--dirsfirst -a)' \
                        '--strip-args=-[set arguments of strip command]:strip-args:(--strip-debug --strip-unneeded)' \
                        + '(verbose_level)' \
                        '-vv[debug mode. many many messages will be output to terminal]' \
                        '-v[verbose mode. many messages will be output to terminal]' \
                        '-q[silent mode. no any messages will be output to terminal]'
                    ;;
                uninstall)
                    _arguments '*:package-name:_ppkg_installed_packages'
                    ;;
                is-available)
                    _arguments '1:package-name:_ppkg_available_packages'
                    ;;
                is-installed)
                    _arguments '1:package-name:_ppkg_installed_packages'
                    ;;
                is-outdated)
                    _arguments '1:package-name:_ppkg_outdated_packages'
                    ;;
                gen-github-workflows)
                    _arguments \
                        '--group-size=-[set the group size]:group-size:(50 40 30 20)' \
                        '--output-dir=-[set the output dir]:output-dir:{_files -/}'
                    ;;
            esac
            ;;
        *);;
    esac
}

_ppkg $@
